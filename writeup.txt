
####################

Ajda Gokcen & Kekoa Riggin
LING 567 (S17)
April 28, 2017
Lab 5

####################

##### PART 0 #####

	For reference, here is the Turkish alphabet, which we are using without any sort of
	transliteration for our grammar:

		AaBbCcÇçDdEeFfGgĞğHhIıİiJjKkLlMmNnOoÖöPpRrSsŞşTtUuÜüVvYyZz

	REMEDIAL CHANGES/ADDITIONS NOT ENTIRELY PERTINENT TO CURRENT ASSIGNMENT:

	1.	case [c], argument optionality [pro-d]

		Before editing the TDL files, we made some more changes to the case choices, grammar, 
		and testsuite.  Namely, we ensured that object drop is allowed for verbs with oblique 
		(non-accusative-case) objects, and disallowed for verbs with accusative-case objects. 
		We added several examples of verbs with oblique objects to the testsuite, including 
		examples of object drop.

		Some examples of a verb that takes dative-case objects, which do indeed receive 
		parses in the baseline grammar, including subject and object drop:

			### DAT object + DAT-TR (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {case}
			Ben				kadına			bayıldım
			Ben				kadın-a			bayıl-dı-m
			pronoun.1SG.NOM	woman.3SG-DAT	adore-PF.NPST-1SG
			'I adored the woman.'

			### DAT-TR SUBJECT DROP (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {argument optionality}
			Kadına			bayıldı
			Kadın-a			bayıl-dı
			woman.3SG-DAT	adore-PF.NPST.3SG
			'He/she/it adored the woman.'

			### DAT-TR OBJECT DROP (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {argument optionality}
			Bayıldı
			Bayıl-dı
			adore-PF.NPST.3SG
			'He/she/it adored.'

		Some examples of a verb that takes ablative-case objects, which do indeed receive 
		parses in the baseline grammar, including object drop:

			### ABL object + ABL-TR (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {case}
			Ben				kadından		korktum
			Ben				kadın-dan		kork-tu-m
			pronoun.1SG.NOM	woman.3SG-ABL	fear-PF.NPST-1SG
			'I feared the woman.'

			### ABL-TR OBJECT DROP (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {argument optionality}
			Korkuyor
			Kork-uyor
			fear-IMPF.NPST.3SG
			'He/she/it is afraid.'

		An example of a verb that takes locative-case objects, which does indeed receive a 
		parse in the baseline grammar:

			### LOC object + LOC-TR (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {case}
			Ben				evde			direttim
			Ben				ev-de			diret-ti-m
			pronoun.1SG.NOM	house.3SG-LOC	insist-PF.NPST-1SG
			'I insisted on the house.'

		Some examples of a verb that takes comitative-case objects (which can be marked either 
		by suffix or via postposition), which do indeed receive parses in the baseline grammar:

			### COM object + COM-TR (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {case}
			Ben				kediyle			ilgilendim
			Ben				kedi-yle		ilgilen-di-m
			pronoun.1SG.NOM	cat.3SG-COM		care-PF.NPST-1SG
			'I was interested in the cat.'

			### COM object + COM-TR (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {case, case-marking adpositions}
			Ben				kedi	ile			ilgilendim
			Ben				kedi	ile			ilgilen-di-m
			pronoun.1SG.NOM	cat.3SG	postpos.COM	care-PF.NPST-1SG
			'I was interested in the cat.'

		An ungrammatical example of a verb that takes accusative-case objects with would-be 
		object dropping, which indeed does NOT receive a parse in the baseline grammar:

			### ACC-TR OBJECT DROP (u) ###
			Source: author
			Vetted: f
			Judgment: u
			Phenomena: {argument optionality}
			Gördü
			Gör-dü
			see-PF.NPST.3SG
			'He/she/it saw.'

		And, finally, an example of something ungrammatical that shouldn't parse but 
		incorrectly receives a parse anyway, with wacky semantics:

			### ABL object + DAT-TR (u) ###
			Source: author
			Vetted: f
			Judgment: u
			Phenomena: {case}
			Ben				kadından		bayıldım
			Ben				kadın-dan		bayıl-dı-m
			pronoun.3SG.NOM	woman.3SG-ABL	adore-PF.NPST-1SG
			'I adored the woman.'

		The verb requires an object in the dative case, but because asyndetic coordination 
		doesn't restrict which cases can combine with which, the would-be subject and the 
		would-be object coordinate and the resultant phrase is apparently nominative (and 
		therefore interpreted as the subject of the verb), so the grammar interprets this 
		sentence as having no explicit object. Again, this should not be possible if 
		coordination were properly restricted only to combine Ns/NPs with matching (or 
		underspecified) case features.

		All we did in the customization system to allow for these examples was to create 
		new types of transitive verbs based on the required case features of their objects, 
		and give these types the features that their subjects be nominative, their objects 
		be whatever the type-specific case must be, and the OPT feature on the object a minus 
		value for accusative-object verbs and a plus for all oblique-object verbs.

	2.	cognitive status [cogst], determiners [det]

		Since definiteness / demonstrative determiners / cognitive status was the only cluster 
		of concepts not really covered in the testsuite prior to this lab, we went ahead and 
		added some related examples to the testsuite before gathering the baseline stats.

		We won't get into detail about the phenomena here, since they are explained in detail 
		below (including the examples from the testsuite), but they were indeed in the 
		testsuite at the time of the baseline coverage/overgeneration calcualtions.

	In general, we changed the particular lexical items in a lot of the testsuite examples 
	in order to have sentences with minimum ambiguity (i.e., we took out "mektubu," which 
	could be either the definite accusative or the nominative 3rd person possessum of 'book,'
	and replaced it with words like "hediyeyi," which is unambiguously the definite accusative 
	form of 'present').

	BASELINE TESTSUITE STATS

		Coverage:

			Positive Items #:		111
			Total Results #:		86
			Word String 0:			3.09
			Lexical Items 0:		8.30
			Distinct Analyses 0:	12.59
			Overall Coverage %:		77.5

		Overgeneration:

			Negative Items #:		62
			Total Results #:		15
			Word String 0:			2.84
			Lexical Items 0:		8.80
			Distinct Analyses 0:	8.00
			Overall Coverage %:		24.2

##### PART 1, 2, 3, & 4 #####
#1:	A descriptive statement of the facts of your language.
#2:	Illustrative IGT examples from your testsuite.
#3:	A statement of how you implemented the phenomenon, in terms of types you added/modified 
#	and particular tdl constraints. That is, I want to see actual tdl snippets with prose 
#	descriptions around them. If the tdl as given on this page worked, please include it 
#	anyway, and note that it wasn't changed. If you needed to make modifications, document 
#	what those were.
#4:	If the analysis is not (fully) working, a description of the problems you are encountering.

	ADJECTIVAL AND ADVERBIAL MODIFIERS {adj, adv}

		1.	Description of linguistic facts

		Attributive adjectives in Turkish must precede the nouns they modify, but they have 
		flexible word order with regard to any quantifiers -- that is, nouns must be the final 
		element of the phrases they head, but attributive adjectives and quantifiers at the 
		front of the phrase are flexibly ordered among themselves.

		Attributive adjectives take no suffixes.  They do not have or need any agreement 
		features with the nouns they modify -- no number, no person, no case, nothing.

		In Turkish, adverbs of manner can appear before or after a saturated VP or at either 
		end of the sentence, suggesting that they can modify VPs and Ss but not Vs.

		2.	Examples from testsuite

		Here are some grammatical configurations of NPs including attributive adjectives:

			### BARE ADJ + NOUN (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {adjectives, word order}
			Güzel		kedi		gördüm
			Güzel		kedi		gör-dü-m
			beautiful	cat.3SG.ACC	see-PF.NPST-1SG
			'I saw a beautiful cat.'

			### DET + ADJ + NOUN (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {adjectives, determiners, word order}
			Bir		güzel		kedi		gördüm
			Bir		güzel		kedi		gör-dü-m
			a.SG	beautiful	cat.3SG.ACC	see-PF.NPST-1SG
			'I saw a beautiful cat.'

			### ADJ + DET + NOUN (g) ###
			# yeah, not gonna get parsed for now
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {adjectives, determiners, word order}
			Güzel		bir		kedi		gördüm
			Güzel		bir		kedi		gör-dü-m
			beautiful	a.SG	cat.3SG.ACC	see-PF.NPST-1SG
			'I saw a beautiful cat.'

		And here are some ungrammatical configurations of NPs including attributive adjectives:

			### BARE NOUN + ADJ (u) ###
			Source: author
			Vetted: f
			Judgment: u
			Phenomena: {adjectives, word order}
			Kedi		güzel		gördüm
			Kedi		güzel		gör-dü-m
			cat.3SG.ACC	beautiful	see-PF.NPST-1SG
			'I saw a beautiful cat.'

			### DET + NOUN + ADJ (u) ###
			Source: author
			Vetted: f
			Judgment: u
			Phenomena: {adjectives, word order}
			Bir		kedi		güzel		gördüm
			Bir		kedi		güzel		gör-dü-m
			a.SG	cat.3SG.ACC	beautiful	see-PF.NPST-1SG
			'I saw a beautiful cat.'

		Here are some examples of grammatical manner adverb usage at the edges of a 
		sentence:

			### ADV + SUBJ + VERB (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {adverbs, word order}
			Hızlı	ben					çıktım
			Hızlı	ben					çık-tı-m
			quickly	pronoun.1SG.NOM		exit-PF.NPST-1SG
			'I left quickly.'

			### SUBJ + VERB + ADV (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {adverbs, word order}
			Ben				çıktım				hızlı
			Ben				çık-tı-m			hızlı
			pronoun.1SG.NOM	exit-PF.NPST-1SG	quickly
			'I left quickly.'

		And here are some examples of grammatical manner adverb usage between the subject and
		the VP:

			### SUBJ + ADV + VERB (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {adverbs, word order}
			Ben				hızlı	çıktım
			Ben				hızlı	çık-tı-m
			pronoun.1SG.NOM	quickly	exit-PF.NPST-1SG
			'I quickly left.'

			### SUBJ + ADV + OBJ + VERB (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {adverbs, word order}
			Ben				hızlı	elma			yedim
			Ben				hızlı	seni			ye-di-m
			pronoun.1SG.NOM	quickly	apple.3SG.ACC	eat-PF-1SG
			'I quickly at the apple.'

		Finally, here is an ungrammatical example where the adverb intervenes between the 
		object/complement and the verb:

			### SUBJ + OBJ + ADV + VERB (u) ###
			Source: author
			Vetted: f
			Judgment: u
			Phenomena: {adverbs, word order}
			Ben				elma			hızlı	yedim
			Ben				elma			hızlı	ye-di-m
			pronoun.1SG.NOM	apple.3SG.ACC	quickly	eat-PF-1SG
			'I quickly ate the apple.'
				
		3.	TDL snippets & descriptions

		Here are the types we defined for adverbs (the former taken verbatim from the 
		assignment instructions page) and added to turkish.tdl:

			> turkish.tdl

			adverb-lex := basic-adverb-lex & intersective-mod-lex &
			  [ SYNSEM [ LOCAL [ CAT [ HEAD.MOD < [ LOCAL.CAT.HEAD verb ]>,
						   VAL [ SPR < >,
							 SUBJ < >,
							 COMPS < >,
							 SPEC < > ]]]]].

			manner-adverb-adv-lex := adverb-lex.

		And here is the single adverbial lexical entry we added to lexicon.tdl:

			> lexicon.tdl

			hızlı := manner-adverb-adv-lex &
			  [ STEM < "hızlı" >,
			    SYNSEM.LKEYS.KEYREL.PRED "_quickly_a_rel" ].

		Here is the rule we added to rules.tdl in order to allow adverbs to appear to the 
		right of the element being modified:

			> rules.tdl

			head-adj-int := head-adj-int-phrase.

		And here is the part of turkish.tdl, which was already present thanks to the 
		customization system, which ensured that the addition of the above rule would not 
		adversely affect adjective distribution (i.e., adjectives still can only appear to 
		the left of the element being modified), since attributive adjectives are defined 
		as POSTHEAD -:

			> turkish.tdl

			basic-adjective_attr-lex-rule := attr-adj-lex-rule & 
                                             basic-adjective_opt_cop-lex-rule-super &
			  [ SYNSEM.LOCAL.CAT [ VAL.SUBJ < >,
								   HEAD.PRD -,
								   POSTHEAD - ] ].

		4.	Problems encountered

		We didn't have any problems, really.  Once we added the adverb types in and the 
		head-adj-int rule, adverbs worked in all the appropriate places and orders, and the 
		basic-adjective_attr-lex-rule prevented any detriment to adjective behavior.

	AGREEMENT BETWEEN ADJECTIVES AND HEAD NOUN {adj, agr}

		Turkish does not have any notion of agreement between adjectives and head nouns.

	DEMONSTRATIVES AND/OR MARKERS OF DEFINITENESS {cogst}

		1.	Description of linguistic facts

		Turkish has an array of optional determiners that select for common nouns with agreeing
		number features (sg/pl, but possibly underpecified).  When there is a determiner in a 
		NP, it must precede the noun, and one can never appear with a determiner.

		The following are numerically-underspecified, definite determiners: her (every),
			bu (this), şu (this/that), o (that)

		The following is a plural, definite determiner: bütün (all)

		The following is the singular, indefinite determiner: bir (a/an)

		The following are plural, indefinite determiners: bazı (some), çok (many)

		Additionally, the overt accusative suffix actually doubles as a specificity marker; 
		there is also a zero accusative suffix which denotes nonspecificity.  All other case 
		suffixes are underspecified for specificity, but the accusative suffixes cannot 
		co-occur with determiners whose specificity feature is different from their own.

		Bare NPs are inherently definite but ambiguous with regard to specificity.

		As for demonstratives, they include the definite determiners "bu" ('this'), "şu" 
		('this/that'), and "o" ('that'), where the first refers to something close to the 
		speaker, the second refers to something close to the hearer, and the third refers to 
		something that is near to neither the speaker nor the hearer.

		Here is a table indicating the cognitive status and specificity features (or lack 
		thereof) associated with all the different common noun cases, pronouns, phrase rules, 
		and determiners:

						COMMON NOUN CASES

							COG-ST			SPECI
			nom (-0)		
			acc (-0)						-
			acc (-(y/n)I)					+
			dat (-(y/n)A)
			abl (-(n)DAn)
			loc	(-(n)DA)
			com (-(y)lA)


							PRONOUNS

							COG-ST			SPECI
			pronouns		activ-or-more	+


						PHRASAL RULES

							COG-ST			SPECI
			bare-np			uniq+fam+act
			dropped subj	in-focus
			dropped obj		type-id


						DETERMINERS

							COG-ST			SPECI
			bu, şu, o		activ+fam		+
			her				uniq+activ+fam	+
			bütün			uniq+active+fam	+
			bir				type-id
			bazı, çok		type-id

		2.	Examples from testsuite

		Examples of indefinite determiner usage with the nonspecific and specific accusatives:

			### indef-det + indef-acc (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {cognitive status, determiners}
			Bir		hediye			getirdim
			Bir		hediye			getir-di-m
			a.SG	present.3SG.ACC	bring-PF.NPST-1SG
			'I brought a present.'

			### indef-det + def-acc -- actually indef+specific (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {cognitive status, determiners}
			Bir		hediyeyi		getirdim
			Bir		hediye-yi		getir-di-m
			a.SG	present.3SG-ACC	bring-PF.NPST-1SG
			'I brought a present.'

		Examples of nonspecific and specific accusatives without determiners:

			### bare zero-acc (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {cognitive status}
			Hediye			getirdim
			Hediye			getir-di-m
			present.3SG.ACC	bring-PF.NPST-1SG
			'I brought a present.'

			### bare zero-acc (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {cognitive status}
			Hediyeyi		getirdim
			Hediye-yi		getir-di-m
			present.3SG-ACC	bring-PF.NPST-1SG
			'I brought the present.'

		Examples of definite determiner usage with the nonspecific accusative (ungrammatical) 
		and specific accusative (grammatical):

			### def-det + indef-acc -- specificity clash (u) ###
			Source: author
			Vetted: f
			Judgment: u
			Phenomena: {cognitive status, determiners}
			Bu		hediye			getirdim
			Bu		hediye			getir-di-m
			this	present.3SG.ACC	bring-PF.NPST-1SG
			'I brought a/this present.'

			### def-det + def-acc (g) ###
			### proximal-demonstrative ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {cognitive status, determiners}
			Bu		hediyeyi		getirdim
			Bu		hediye-yi		getir-di-m
			this	present.3SG-ACC	bring-PF.NPST-1SG
			'I brought this present.'

		And finally, since the last two examples showed the proximal demonstrative determiner 
		"bu," here are some examples with the distal/hearer and distal/remote determiners:

			### distal/hearer-demonstrative ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {cognitive status, determiners}
			Şu		hediyeyi		getirdim
			Şu		hediye-yi		getir-di-m
			this	present.3SG-ACC	bring-PF.NPST-1SG
			'I brought this/that present.'

			### distal/remote-demonstrative ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {cognitive status, determiners}
			O		hediyeyi		getirdim
			O		hediye-yi		getir-di-m
			that	present.3SG-ACC	bring-PF.NPST-1SG
			'I brought that present.'

		3.	TDL snippets & descriptions

		DEFINITENESS & SPECIFICITY OUTSIDE OF DETERMINERS

		Here are the accusative rules in the Lexical Rules section of turkish.tdl, modified 
		from the customization system's output to add in the SPECI features (all other case 
		rules remained the same, since only the accusative variants affect specificity):

			> turkish.tdl

			spec-acc-lex-rule-lex-rule := case-common-noun-lex-rule-super & infl-lex-rule &
			  [ SYNSEM.LOCAL [ CAT.HEAD [ CASE acc,
										  CASE-MARKED + ],
							   CONT.HOOK.INDEX.SPECI + ] ].

			nonspec-acc-lex-rule-lex-rule := case-common-noun-lex-rule-super & const-lex-rule &
			  [ SYNSEM.LOCAL [ CAT.HEAD [ CASE acc,
										  CASE-MARKED + ],
							   CONT.HOOK.INDEX.SPECI - ] ].

		And here is the supertype from which all pronouns in the lexicon inherit (not 
		directly, but through subtypes thereof), so all of them have "activ-or-more" COG-ST 
		features and positive SPECI features, per the assignment instructions:

			> turkish.tdl

			personal-pronoun-lex := no-spr-noun-lex &
			  [ SYNSEM.LOCAL [ CONT.HOOK.INDEX [ COG-ST activ-or-more,
												 SPECI + ],
							   CAT.HEAD.PRON + ] ].

		This supertype has subtypes based on person and number features, e.g.:

			> turkish.tdl

			1sg-pronoun-noun-lex := personal-pronoun-lex &
			  [ SYNSEM.LOCAL.CONT.HOOK.INDEX.PNG [ PER 1st,
												   NUM sg ] ].

		And the types that the pronominal lexical entries directly inherit from further 
		distinguish case features (since pronominal case is pretty irregular), e.g.:

			> turkish.tdl

			nom-1sg-pronoun-noun-lex := 1sg-pronoun-noun-lex &
			  [ SYNSEM.LOCAL.CAT.HEAD [ CASE nom,
										CASE-MARKED + ] ].

		Here's the lexical entry inheriting from the above example type (and thereby 
		from personal-pronoun-lex, so it gets the proper COG-ST and SPECI features):

			> lexicon.tdl

			ben_1 := nom-1sg-pronoun-noun-lex &
			  [ STEM < "ben" >,
				SYNSEM.LKEYS.KEYREL.PRED "_pronoun_n_rel" ].

		And here are the bare NP phrases in turkish.tdl, of which the subtypes apply to 
		pronouns and common nouns respectively, since pronouns and phrases headed by them are 
		[COG-ST activ-or-more] and [SPECI +], while common-noun-headed bare NPs are treated 
		as definite in Turkish, i.e., [COG-ST uniq+fam+act]:

			> turkish.tdl

			bare-np-phrase := basic-bare-np-phrase &
			  [ C-CONT.RELS <! [ PRED "exist_q_rel" ] !> ].

			bare-pronoun-np-phrase := bare-np-phrase &
			  [ HEAD-DTR.SYNSEM.LOCAL.CAT.HEAD.PRON + ].

			bare-common-np-phrase := bare-np-phrase &
			  [ HEAD-DTR.SYNSEM.LOCAL.CAT.HEAD.PRON -,
				SYNSEM.LOCAL.CONT.HOOK.INDEX.COG-ST uniq+fam+act ].

		In order for these phrases to only apply to pronouns and common nouns, respectively, 
		we created a PRON feature, i.e., in the Matrix Type Addenda of turkish.tdl:

			> turkish.tdl

			head :+ [ PRD bool,
					  FORM form,
					  AUX bool,
					  PRON bool ].

		And then the types where the proper values of this feature are given to pronouns and 
		common nouns:

			> turkish.tdl

			common-noun-lex := noun-lex & case-common-noun-rule-dtr & poss-common-noun-rule-dtr &
			  [ SYNSEM.LOCAL [ CONT.HOOK.INDEX.PNG.PER 3rd,
							   CAT.HEAD.PRON - ],
				INFLECTED [ CASE-COMMON-NOUN-FLAG -,
							NUMBER-COMMON-NOUN-FLAG - ] ].

			personal-pronoun-lex := no-spr-noun-lex &
			  [ SYNSEM.LOCAL [ CONT.HOOK.INDEX [ COG-ST activ-or-more,
												 SPECI + ],
							   CAT.HEAD.PRON + ] ].

		DEFINITENESS, SPECIFICITY, & DEMONSTRATIVITY OF DETERMINERS

		Here are the types (verbatim from the assignment instructions) we added to the Matrix 
		Type Addenda section of turkish.tdl:

			> turkish.tdl

			demonstrative_a_rel := predsort.
			proximal+dem_a_rel := demonstrative_a_rel. ; close to speaker
			distal+dem_a_rel := demonstrative_a_rel.   ; away from speaker
			remote+dem_a_rel := distal+dem_a_rel.      ; away from speaker and hearer
			hearer+dem_a_rel := distal+dem_a_rel.      ; near hearer

		Here is the lexical type defined in turkish.tdl in order to get around the current 
		version of basic-determiner-lex (verbatim from the assignment instructions except 
		for the additional specification of an empty MOD list to prevent determiners from 
		acting as modifiers):

			> turkish.tdl

			determiner-lex-supertype := norm-hook-lex-item & 
										basic-zero-arg & 
										no-icons-lex-item &
			  [ SYNSEM [ LOCAL [ CAT [ HEAD det & [ MOD < > ],
						   VAL [ SPEC.FIRST.LOCAL.CONT.HOOK [ INDEX #ind,
															  LTOP #larg ],
											SPR < >,
											SUBJ < >,
											COMPS < >] ],
						 CONT.HCONS < ! qeq &
							 [ HARG #harg,
							   LARG #larg ] ! > ], 
					 LKEYS.KEYREL quant-relation &
					   [ ARG0 #ind,
						 RSTR #harg ] ] ].

		Here are the types which now inherit from the new determiner-lex-supertype instead of 
		from the originally given determiner-lex (which in turn inherited from 
		basic-determiner-lex):

			> turkish.tdl

			demonstrative-determiner-lex := determiner-lex-supertype & 
			  [ SYNSEM 
				 [ LKEYS.ALTKEYREL #evrel,
				   LOCAL [ CAT.VAL.SPEC.FIRST.LOCAL.CONT.HOOK [ INDEX #ind & [ COG-ST activ+fam, 
																			   SPECI + ],
																LTOP #ltop ],
						   CONT.RELS <! [ PRED "exist_q_rel" ],
										#evrel & arg1-ev-relation & [ LBL #ltop, 
																	  ARG1 #ind ] !> ] ] ].

			ordinary-determiner-lex := determiner-lex-supertype & single-rel-lex-item.

		The demonstrative pronouns in the lexicon inherit directly from the above 
		demonstrative-determiner-lex, but ordinary-determiner-lex has further subtypes based 
		on definiteness (COG-ST) and specificity (SPECI), with definiteness and indefiniteness 
		defined featurally per the table in the assignment description, shown here:

			> turkish.tdl

			definite-determiner-lex := ordinary-determiner-lex & 
			  [ SYNSEM.LOCAL.CONT.HOOK.INDEX [ COG-ST uniq+fam+act, SPECI + ] ].

			indefinite-determiner-lex := ordinary-determiner-lex &
			  [ SYNSEM.LOCAL.CONT.HOOK.INDEX.COG-ST type-id ].

		The types which the ordinary determiners' lexical entries immediately inherit from 
		are further specified for number agreement with the head noun.  Those subtypes are 
		shown here (also in turkish.tdl):

			> turkish.tdl

			sgpl-definite-determiner-lex := definite-determiner-lex.

			pl-definite-determiner-lex := definite-determiner-lex &
			  [ SYNSEM.LOCAL.CAT.VAL.SPEC.FIRST.LOCAL.CONT.HOOK.INDEX.PNG.NUM pl ].

			sg-indefinite-determiner-lex := indefinite-determiner-lex &
			  [ SYNSEM.LOCAL.CAT.VAL.SPEC.FIRST.LOCAL.CONT.HOOK.INDEX.PNG.NUM sg ].

			pl-indefinite-determiner-lex := indefinite-determiner-lex &
			  [ SYNSEM.LOCAL.CAT.VAL.SPEC.FIRST.LOCAL.CONT.HOOK.INDEX.PNG.NUM pl ].

		Finally, here are the lexical entries for the demonstrative determiners, with 
		appropriately adjusted PRED values and inherited types, in lexicon.tdl:

			> lexicon.tdl

			bu_4 := demonstrative-determiner-lex &
			  [ STEM < "bu" >,
				SYNSEM.LKEYS.ALTKEYREL.PRED proximal+dem_a_rel ].

			şu_4 := demonstrative-determiner-lex &
			  [ STEM < "şu" >,
				SYNSEM.LKEYS.ALTKEYREL.PRED hearer+dem_a_rel ].

			o_4 := demonstrative-determiner-lex &
			  [ STEM < "o" >,
				SYNSEM.LKEYS.ALTKEYREL.PRED remote+dem_a_rel ].

		...and for the ordinary determiners:

			> lexicon.tdl

			her := sgpl-definite-determiner-lex &
			  [ STEM < "her" >,
				SYNSEM.LKEYS.KEYREL.PRED "_every_q_rel" ].

			bütün := pl-definite-determiner-lex &
			  [ STEM < "bütün" >,
				SYNSEM.LKEYS.KEYREL.PRED "_all_q_rel" ].

			bir := sg-indefinite-determiner-lex &
			  [ STEM < "bir" >,
				SYNSEM.LKEYS.KEYREL.PRED "_a_q_rel" ].

			bazı := pl-indefinite-determiner-lex &
			  [ STEM < "bazı" >,
				SYNSEM.LKEYS.KEYREL.PRED "_some_q_rel" ].

			çok := pl-indefinite-determiner-lex &
			  [ STEM < "çok" >,
				SYNSEM.LKEYS.KEYREL.PRED "_many_q_rel" ].

		4.	Problems encountered

		Everything worked in the end, but it took a bit for us to figure out (with help) how 
		to distinguish between pronouns and common nouns -- that is, how to enforce a 
		distinction with a new feature!

	ARGUMENT OPTIONALITY {pro-d}

		1.	Description of linguistic facts

		Because Turkish is a pro-drop language, the subject is always optional. Person and
		number agreement inflection is required on the verb regardless. Objects of VPs cannot
		be dropped if the verb requires an accusative direct object, but they can be dropped 
		if the verb takes an oblique object of a different case.

		As far as the cognitive status of dropped arguments goes, the subject, if dropped, 
		is in-focus, which is why verbs with first or second person subject-agreement suffixes 
		can always go without explicit subjects.  Dropped oblique objects are assumed to be 
		activ-or-more, though frankly we were not able to discern this very clearly from 
		available data.

		2.	Examples from testsuite

		Examples of argument optionality, first showing the explicit subject for reference, 
		for both intransitive and accusative-transitive verbs:

			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {word order}
			Kadın           çıktı
			Kadın           çık-tı
			woman.3SG.NOM   exit-PF.3SG
			'The woman left.'

			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {word order}
			Kadın			kediyi		gördü
			Kadın			kedi-yi		gör-dü
			woman.3SG.NOM	cat.3SG-ACC	see-PF.NPST.3SG
			'The woman saw the cat.'

		...and then remembering, of course, that the subject is optional for both verb types:

			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {argument optionality}
			Çıktı
			Çık-tı
			exit-PF.3SG
			'He/she/it left.'

			### ACC-TR SUBJ-DROP (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {argument optionality}
			Kediyi		gördü
			Kedi-yi		gör-dü
			cat.3SG-ACC	see-PF.NPST.3SG
			'He/she/it saw the cat.'

		The first example follows all other rules implemented in our grammar and includes the
		verb's subject. The second example follows all other grammar rules but excludes the
		subject of the verb. All contain the required agreement feature on the verb (zero
		for 3rd person singular).

		Here is an example of a verb that takes a dative oblique object with the subject 
		dropped, and then with the object dropped as well:

			### DAT-TR SUBJECT DROP (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {argument optionality}
			Kadına			bayıldı
			Kadın-a			bayıl-dı
			woman.3SG-DAT	adore-PF.NPST.3SG
			'He/she/it adored the woman.'

			### DAT-TR OBJECT DROP (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {argument optionality}
			Bayıldı
			Bayıl-dı
			adore-PF.NPST.3SG
			'He/she/it adored.'

		Probably a better translation of the above sentence would be "he/she/it was smitten."

		And another example of a verb that takes an ablative oblique object, also with the 
		object dropped:

			### ABL-TR OBJECT DROP (g) ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {argument optionality}
			Korkuyor
			Kork-uyor
			fear-IMPF.NPST.3SG
			'He/she/it is afraid.'

		And in all of these object drop cases the would-be object is assumed to be 
		activ-or-more.

		And finally, an ungrammatical example of an accusative-transitive verb ostensibly 
		dropping its object (but it cannot and indeed does not receive a parse because this 
		type of transitive verb does not have an optional object):

			### ACC-TR OBJECT DROP (u) ###
			Source: author
			Vetted: f
			Judgment: u
			Phenomena: {argument optionality}
			Gördü
			Gör-dü
			see-PF.NPST.3SG
			'He/she/it saw.'

		3.	TDL snippets & descriptions

		Here are the types that include the OPT-CS features giving dropped (oblique) 
		objects the proper [ COG-ST activ-or-more ] status:

			> turkish.tdl

			dat-tr-verb-lex := transitive-verb-lex &
			  [ SYNSEM.LOCAL.CAT.VAL [ SUBJ.FIRST.LOCAL.CAT.HEAD [ CASE nom,
																   CASE-MARKED + ],
									   COMPS.FIRST [ LOCAL.CAT.HEAD [ CASE dat,
																	  CASE-MARKED + ],
													 OPT +,
													 OPT-CS activ-or-more ] ] ].

			abl-tr-verb-lex := transitive-verb-lex &
			  [ SYNSEM.LOCAL.CAT.VAL [ SUBJ.FIRST.LOCAL.CAT.HEAD [ CASE nom,
																   CASE-MARKED + ],
									   COMPS.FIRST [ LOCAL.CAT.HEAD [ CASE abl,
																	  CASE-MARKED + ],
													 OPT +,
													 OPT-CS activ-or-more ] ] ].

			loc-tr-verb-lex := transitive-verb-lex &
			  [ SYNSEM.LOCAL.CAT.VAL [ SUBJ.FIRST.LOCAL.CAT.HEAD [ CASE nom,
																   CASE-MARKED + ],
									   COMPS.FIRST [ LOCAL.CAT.HEAD [ CASE loc,
																	  CASE-MARKED + ],
													 OPT +,
													 OPT-CS activ-or-more ] ] ].

			com-tr-verb-lex := transitive-verb-lex &
			  [ SYNSEM.LOCAL.CAT.VAL [ SUBJ.FIRST.LOCAL.CAT.HEAD [ CASE nom,
																   CASE-MARKED + ],
									   COMPS.FIRST [ LOCAL.CAT.HEAD [ CASE com,
																	  CASE-MARKED + ],
													 OPT +,
													 OPT-CS activ-or-more ] ] ].

		And just for good measure, the snippet that prevents accusative-transitive verbs 
		from dropping their objects at all:

			> turkish.tdl

			acc-tr-verb-lex := nom-acc-transitive-verb-lex &
			  [ SYNSEM.LOCAL.CAT.VAL [ SUBJ.FIRST.LOCAL.CAT.HEAD [ CASE nom,
																   CASE-MARKED + ],
									   COMPS.FIRST [ LOCAL.CAT.HEAD [ CASE acc,
																	  CASE-MARKED + ],
													 OPT - ] ] ].

		The only other snippets pertaining to argument optionality are where the rules are 
		instantiated in rules.tdl:

			> rules.tdl

			decl-head-opt-subj := decl-head-opt-subj-phrase.

			basic-head-opt-comp := basic-head-opt-comp-phrase.

		...and the declaration of decl-head-opt-subj-phrase in matrix.tdl inherits from 
		basic-head-opt-subj-phrase, also in matrix.tdl, where the dropped subject's 
		[ COG-ST in-foc ] is specified:

			> matrix.tdl (unchanged, of course)

			basic-head-opt-subj-phrase := head-valence-phrase & head-only &
			  [ INFLECTED #infl,
				SYNSEM canonical-synsem &
					  [ LOCAL.CAT [ VAL [ SUBJ < >,
							  COMPS #comps,
							  SPR #spr,
							  SPEC #spec ],
							POSTHEAD #ph ],
					MODIFIED #mod ],
				HEAD-DTR [ INFLECTED #infl & infl-satisfied,
					   SYNSEM [ LOCAL [ CAT [ HEAD.MOD olist,
											  VAL [ SUBJ < unexpressed-reg &
									   [ OPT +,
										 LOCAL.CONT.HOOK [ INDEX #index & [ COG-ST in-foc ],
												   ICONS-KEY #ikey,
												   CLAUSE-KEY #ckey ] ] >,
									COMPS #comps,
									SPR #spr,
									SPEC #spec ],
								  POSTHEAD #ph ],
							CONT.HOOK.INDEX event ],
						MODIFIED #mod ] ],
				C-CONT [ RELS <! !>,
					 HCONS <! !>,
					 ICONS <! #ikey & non-focus & [ IARG1 #ckey,
									IARG2 #index ] !> ] ].

			; ERB 2007-01-21 Subtypes for declaratives and imperatives

			decl-head-opt-subj-phrase := basic-head-opt-subj-phrase & declarative-clause &
			  [ SYNSEM.LOCAL.CAT.MC #mc,
				HEAD-DTR.SYNSEM.LOCAL.CAT.MC #mc ].

		4.	Problems encountered

		The only real problem we encountered is it not being entirely clear what the cognitive 
		status of dropped objects are.  We couldn't find resources that discussed it, and 
		available native speakers weren't familiar enough with the concept to help.

	POSSESSIVES {poss}

		1.	Description of linguistic facts

		Turkish allows for an optional possessor (which must have genitive case) on either 
		(left or right) edge of the NP, along with a required (head noun) possessum that must 
		be inflected with a possessive suffix whose number and person features agree with those 
		of the possessor.

		2.	Examples from testsuite

		Here are some examples of possessive constructions with both the required suffix on the 
		possessum and an explicit genitive pronoun agreeing with it, varying by whether the 
		possessor comes before or after the possessum:

			### 1SG, with genitive ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {possessives}
			Benim			ailem					geldi
			benim			aile-m					gel-di
			pronoun.1SG.GEN	family.3SG.NOM-POSS.1SG	come-PF.3SG
			'My family arrived.'

			### 1SG, with genitive, possessive before genitive ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {possessives, word order}
			Ailem					benim			geldi
			Aile-m					benim			gel-di
			family.3SG.NOM-1SGPOSS	pronoun.1SG.GEN	come-PF.NPST.3SG
			'My family arrived.'

		And here is a similar (and still grammatical) example, but with the genitive pronoun 
		omitted:

			### 1SG, without genitive ###
			Source: author
			Vetted: f
			Judgment: g
			Phenomena: {possessives}
			Ailem					geldi
			Aile-m					gel-di
			family.3SG.NOM-POSS.1SG	come-PF.3SG
			'My family arrived.'

		Finally, here is an ungrammatical example demonstrating that the possessive inflection 
		on the possessum is required:

			### 1SG, without possessive ###
			Source: author
			Vetted: f
			Judgment: u
			Phenomena: {possessives}
			Benim			aile			geldi
			benim			aile			gel-di
			pronoun.1SG.GEN	family.3SG.NOM	come-PF.3SG
			'My family arrived.'

		3.	TDL snippets & descriptions

		Here is the absolutely unwieldy bit of code that allows for the possessum to be 
		inflected; it is as unwieldy as it is because we needed to inherit from a valence 
		changing type but infl-val-change-only-lex-rule prevented C-CONT from being 
		non-empty, and also because Turkish inflectional morphology is so complicated. 
		The case-common-noun-rule-dtr supertype means that this rule applies just prior to 
		the case inflectional rules being applied.

			> turkish.tdl

			poss-common-noun-lex-rule-super := same-ctxt-lex-rule &
											   same-cont-lex-rule &
											   same-agr-lex-rule &
											   same-head-lex-rule &
											   same-hc-light-lex-rule &
											   same-posthead-lex-rule &
											   same-mc-lex-rule &
											   infl-lex-rule &
											   case-common-noun-rule-dtr &
			  [ INFLECTED [ POSS-FLAG +,
							CASE-COMMON-NOUN-FLAG #case-common-noun,
							NEG-LEX-RULE-FLAG #neg-lex-rule,
							TENSE-ADJECTIVE-FLAG #tense-adjective,
							BASIC-ADJECTIVE_OPT_COP-FLAG #basic-adjective_opt_cop,
							TENSE-VERB-FLAG #tense-verb,
							AGREEMENT-ADJECTIVE-FLAG #agreement-adjective,
							ASPECT-VERB-FLAG #aspect-verb,
							AGREEMENT-VERB-FLAG #agreement-verb,
							NUMBER-COMMON-NOUN-FLAG #number-common-noun,
							POLARITY-VERB-FLAG #polarity-verb,
							AGREEMENT-COPULA-FLAG #agreement-copula,
							POLARITY-COPULA-FLAG #polarity-copula,
							TENSE-COPULA-FLAG #tense-copula,
							NEG-COP-LEX-RULE-FLAG #neg-cop-lex-rule,
							ASPECT-COPULA-FLAG #aspect-copula ],
			   SYNSEM [ LKEYS.ALTKEYREL.ARG0.PNG #png,
						LOCAL.CAT.VAL [ SPR < [ LOCAL [ CAT [ HEAD noun & [ CASE gen ] ],
											  CONT.HOOK.INDEX #index2 ] ] >, 
										SPEC #spec,
										COMPS #comps,
										SUBJ #subj ] ],
			   DTR poss-common-noun-rule-dtr & [ SYNSEM.LOCAL [ CONT.HOOK [ INDEX #index,
																			LTOP #lbl ],
													CAT.VAL [ SPEC #spec,
															  COMPS #comps,
															  SUBJ #subj ] ],
				   INFLECTED [ CASE-COMMON-NOUN-FLAG #case-common-noun,
							   NEG-LEX-RULE-FLAG #neg-lex-rule,
							   TENSE-ADJECTIVE-FLAG #tense-adjective,
							   BASIC-ADJECTIVE_OPT_COP-FLAG #basic-adjective_opt_cop,
							   TENSE-VERB-FLAG #tense-verb,
							   AGREEMENT-ADJECTIVE-FLAG #agreement-adjective,
							   ASPECT-VERB-FLAG #aspect-verb,
							   AGREEMENT-VERB-FLAG #agreement-verb,
							   NUMBER-COMMON-NOUN-FLAG #number-common-noun,
							   POLARITY-VERB-FLAG #polarity-verb,
							   AGREEMENT-COPULA-FLAG #agreement-copula,
							   POLARITY-COPULA-FLAG #polarity-copula,
							   TENSE-COPULA-FLAG #tense-copula,
							   NEG-COP-LEX-RULE-FLAG #neg-cop-lex-rule,
							   ASPECT-COPULA-FLAG #aspect-copula ] ],
			   C-CONT [ HOOK [ INDEX #index,
					   LTOP #lbl ],
					RELS < ! arg12-ev-relation &
					  [ PRED "poss_rel",
						LBL #lbl,
						ARG1 #index,
						ARG2 #index2 & [ PNG #png ] ] ! >,
					ICONS < ! ! > ] ].

		This should ostensibly make it so that possession-inflected NPs look for genitive 
		noun specifiers, but see the "Problems encountered" section for an explanation that, 
		basically, "it doesn't work and we don't know why."

		Note that the pronoun and existence relations are not in the RELS list, because 
		those may be supplied later on by either a bare-NP-like phrase or by the genitive 
		possessor specifier.

		To that end, here is the bare-NP-like phrase that allows inflected possessa to 
		appear without any specifier:

			> turkish.tdl

			bare-poss-np-phrase := head-only & head-valence-phrase &
			  [ SYNSEM.LOCAL.CAT.VAL [ SPR < >,
						   SUBJ < >,
						   COMPS < >,
						   SPEC < > ],
				HEAD-DTR.SYNSEM [ LOCAL [ CAT [ HEAD noun,
							  VAL [ SPR < unexpressed & 
										  [ LOCAL.CAT [ HEAD noun & [ CASE gen ] ],
									OPT + ] >,
								SUBJ < >,
								COMPS < > ] ],
							CONT.HOOK [ INDEX #index,
										LTOP #larg ] ],
							LKEYS.ALTKEYREL #altkeyrel ],
				C-CONT [ RELS <! quant-relation &
								 [ PRED "exist_q_rel",
								   ARG0 #index, 
								   RSTR #harg,
								   BODY #larg ],
								 quant-relation &
								 [ PRED "exist_q_rel",
								   ARG0 #arg2,
								   RSTR #harg2 ],
								 #altkeyrel & noun-relation &
								 [ PRED "pron_rel",			 
								   LBL #larg2,
								   ARG0 #arg2 & [ COG-ST activ-or-more,
												  SPECI + ] ] !>,
					 HCONS <! qeq & 
							  [ HARG #harg,
								LARG #larg ],
							  qeq &
							  [ HARG #harg2,
								LARG #larg2 ] !>,
					 ICONS <! !>,
					 HOOK [ INDEX #index ] ] ].

		Here are the person-and-number-specific subtypes of the above rule:

			> turkish.tdl

			1sg-poss-lex-rule-lex-rule := poss-common-noun-lex-rule-super &
			  [ SYNSEM.LKEYS.ALTKEYREL.ARG0.PNG [ PER 1st, NUM sg ] ].

			1pl-poss-lex-rule-lex-rule := poss-common-noun-lex-rule-super &
			  [ SYNSEM.LKEYS.ALTKEYREL.ARG0.PNG [ PER 1st, NUM pl ] ].

			2sg-poss-lex-rule-lex-rule := poss-common-noun-lex-rule-super &
			  [ SYNSEM.LKEYS.ALTKEYREL.ARG0.PNG [ PER 2nd, NUM sg ] ].

			2pl-poss-lex-rule-lex-rule := poss-common-noun-lex-rule-super &
			  [ SYNSEM.LKEYS.ALTKEYREL.ARG0.PNG [ PER 2nd, NUM pl ] ].

			3sgpl-poss-lex-rule-lex-rule := poss-common-noun-lex-rule-super &
			  [ SYNSEM.LKEYS.ALTKEYREL.ARG0.PNG [ PER 3rd ] ].

			3pl-poss-lex-rule-lex-rule := poss-common-noun-lex-rule-super &
			  [ SYNSEM.LKEYS.ALTKEYREL.ARG0.PNG [ PER 3rd, NUM pl ] ].

		Here is the case inflection rule from which the genitive inflection rule inherits 
		(unchanged from the customization system's output):

			> turkish.tdl

			case-common-noun-lex-rule-super := add-only-no-ccont-rule &
			  [ INFLECTED [ CASE-COMMON-NOUN-FLAG +,
							NEG-LEX-RULE-FLAG #neg-lex-rule,
							POSS-FLAG #poss,
							TENSE-ADJECTIVE-FLAG #tense-adjective,
							BASIC-ADJECTIVE_OPT_COP-FLAG #basic-adjective_opt_cop,
							TENSE-VERB-FLAG #tense-verb,
							AGREEMENT-ADJECTIVE-FLAG #agreement-adjective,
							ASPECT-VERB-FLAG #aspect-verb,
							AGREEMENT-VERB-FLAG #agreement-verb,
							NUMBER-COMMON-NOUN-FLAG #number-common-noun,
							POLARITY-VERB-FLAG #polarity-verb,
							AGREEMENT-COPULA-FLAG #agreement-copula,
							POLARITY-COPULA-FLAG #polarity-copula,
							TENSE-COPULA-FLAG #tense-copula,
							NEG-COP-LEX-RULE-FLAG #neg-cop-lex-rule,
							ASPECT-COPULA-FLAG #aspect-copula ],
				DTR case-common-noun-rule-dtr &
					[ INFLECTED [ NEG-LEX-RULE-FLAG #neg-lex-rule,
								  POSS-FLAG #poss,
								  TENSE-ADJECTIVE-FLAG #tense-adjective,
								  BASIC-ADJECTIVE_OPT_COP-FLAG #basic-adjective_opt_cop,
								  TENSE-VERB-FLAG #tense-verb,
								  AGREEMENT-ADJECTIVE-FLAG #agreement-adjective,
								  ASPECT-VERB-FLAG #aspect-verb,
								  AGREEMENT-VERB-FLAG #agreement-verb,
								  NUMBER-COMMON-NOUN-FLAG #number-common-noun,
								  POLARITY-VERB-FLAG #polarity-verb,
								  AGREEMENT-COPULA-FLAG #agreement-copula,
								  POLARITY-COPULA-FLAG #polarity-copula,
								  TENSE-COPULA-FLAG #tense-copula,
								  NEG-COP-LEX-RULE-FLAG #neg-cop-lex-rule,
								  ASPECT-COPULA-FLAG #aspect-copula ] ] ].

		...and the genitive inflectional rule in particular:

			> turkish.tdl

			gen-lex-rule-lex-rule := case-common-noun-lex-rule-super & infl-lex-rule &
			  [ SYNSEM.LOCAL.CAT.HEAD [ CASE gen,
										CASE-MARKED + ] ].

		As well as the specific suffixes for the rule (from irules.tdl):

			> irules.tdl

			gen-lex-rule-suffix1 :=
			%suffix (* nin)
			gen-lex-rule-lex-rule.

			gen-lex-rule-suffix2 :=
			%suffix (* nın)
			gen-lex-rule-lex-rule.

			gen-lex-rule-suffix3 :=
			%suffix (* nun)
			gen-lex-rule-lex-rule.

			gen-lex-rule-suffix4 :=
			%suffix (* nün)
			gen-lex-rule-lex-rule.

			gen-lex-rule-suffix5 :=
			%suffix (* in)
			gen-lex-rule-lex-rule.

			gen-lex-rule-suffix6 :=
			%suffix (* ın)
			gen-lex-rule-lex-rule.

			gen-lex-rule-suffix7 :=
			%suffix (* un)
			gen-lex-rule-lex-rule.

			gen-lex-rule-suffix8 :=
			%suffix (* ün)
			gen-lex-rule-lex-rule.

		And since we declared all pronominal inflections as separate lexical entries because 
		of their irregularities, here is the most immediately inherited type for the 1SG 
		genitive pronoun, as an example:

			> turkish.tdl

			gen-1sg-pronoun-noun-lex := 1sg-pronoun-noun-lex &
			  [ SYNSEM.LOCAL.CAT.HEAD [ CASE gen,
										CASE-MARKED + ] ].

		And its declaration in the lexicon:

			> lexicon.tdl

			benim := gen-1sg-pronoun-noun-lex &
			  [ STEM < "benim" >,
				SYNSEM.LKEYS.KEYREL.PRED "_pronoun_n_rel" ].

		Here is the section of rules.tdl where we defined a spec-head rule in addition to 
		the head-spec rule, to allow for the flexibility of genitive possessors being on 
		either the right or the left of the noun:

			> rules.tdl

			head-spec := head-spec-phrase.

			spec-head := spec-head-phrase.

		And here is the section where we defined the bare possessive NP (-like) phrase:

			> rules.tdl

			bare-poss-np := bare-poss-np-phrase.

		4.	Problems encountered

		Oh, golly.  This part was tough and we definitely did not get it working.

		Inflectional marking of possession sans the genitive does parse!

		The semantics in the case of the bare-NP-like possessive are mostly correct, but 
		we could not figure out how to identify the ARG0 of the pron_rel with the ARG2 
		of the poss_rel.  ARG2 of the poss_rel has the right PNG, features at least, while 
		ARG0 of the pron_rel has the proper [ COG-ST activ-or-more ].

		...but we could not get the head-spec rule to combine the possessors and possessa. 
		Presumably the issue lies in the unwieldy poss-common-noun-lex-rule-super, which is 
		where the [ HEAD noun & [ CASE gen ] ] features of inflected possessa are supposed 
		to originate.

		At one point the genitive possessors seemed to work, but then we realized that it 
		was actually combining via head-comp, which was very weird.  The current version 
		of the grammar does not have this behavior.

	OTHER

		Coordination was adding way too many parses, so as we worked we commented out all 
		of the many, many coordination and coordination agreement rules in rules.tdl, e.g.,

			> rules.tdl

			;n1-1st-any-1st-any-any-pl-top-coord := n1-1st-any-1st-any-any-pl-top-coord-rule.
			(...)
			;s4-bottom-coord := s4-bottom-coord-rule.

		We un-commented them before getting the final results, but omitting them vastly 
		fixed a lot of our overgeneration problems.

##### PART 5 #####
#	In addition, your write up should include a statement of the current coverage of your 
#	grammar over your test suite (using numbers you can get from Analyze | Coverage and 
#	Analyze | Overgeneration in [incr tsdb()]) and a comparison between your baseline test 
#	suite run and your final one for this lab (see Compare | Competence).

	BASELINE GRAMMAR TESTSUITE STATS

		Coverage:

			Positive Items #:		112
			Total Results #:		86
			Word String 0:			3.09
			Lexical Items 0:		8.30
			Distinct Analyses 0:	12.59
			Overall Coverage %:		77.5

		Overgeneration:

			Negative Items #:		61
			Total Results #:		15
			Word String 0:			2.84
			Lexical Items 0:		8.80
			Distinct Analyses 0:	8.00
			Overall Coverage %:		24.2

	FINAL GRAMMAR TESTSUITE STATS

		Coverage:

			Positive Items #:		112
			Total Results #:		91
			Word String 0:			3.09
			Lexical Items 0:		7.71
			Distinct Analyses 0:	15.97
			Overall Coverage %:		81.2

		Overgeneration:

			Negative Items #:		61
			Total Results #:		14
			Word String 0:			2.84
			Lexical Items 0:		7.96
			Distinct Analyses 0:	8.64
			Overall Coverage %:		23.0

	FROM COMPARE | COMPETENCE

		Baseline:

			Lexical:				2.71
			Analyses:				11.91
			In:						77.5
			Out:					24.2

		Final:

			Lexical:				2.52
			Analyses:				14.99
			In:						81.2
			Out:					23.0

##### PART 6 #####
#	Finally please briefly describe your test corpus, including: where you collected it, how 
#	many sentences it contains, and what format (transliterated, etc) it is in.

	The test corpus was gathered from the PDF available at the following link:

		http://www.ege-edebiyat.org/docs/519.pdf

	...which originally comes from the following source:

		Felek, Burhan. Nasrettin Hoca, Milliyet Yayınları, İstanbul, 1983.

	And it's basically a collection of sentences from this written version of the tales of 
	Nasreddin Hodja, so it's got a lot of literary (i.e., evidential) perfective verbs and 
	that sort of thing.

	The format is exactly like that of the testsuite, with the text essentially unchanged from 
	the document, but with author-added glosses and translations.  There are XXX items in all. 
	Here's a sampling:

		### 1 ###
		Source: a
		Vetted: f
		Judgment: g
		Phenomena: {}
		Dükkancı			çıkarıp			vermiş
		Dükkancı			çıkar-ıp		ver-miş
		shopkeeper.3SG.NOM	remove-CONV		give-EVPF.NPST.3SG
		'The shopkeeper took it out and handed it over.'

		### 7 ###
		Source: a
		Vetted: f
		Judgment: g
		Phenomena: {}
		Hoca'nın		bir		kuzusu				var			imiş
		Hoca-nın		bir		kuzu-su				var			i-miş
		Hodja.3SG-GEN	a.SG	lamb.3SG-3SGPOSS	existent	be-EVPF.NPST.3SG
		'Hodja had a lamb.'

		### 14 ###
		Source: a
		Vetted: f
		Judgment: g
		Phenomena: {}
		Kasaptan		iki	kilo		et		alıp		evine					getirmiş
		Kasap-tan		iki	kilo		et		al-ıp		ev-i-ne					getir-miş
		butcher.3SG-ABL	two	kilograms	meat	take-CONV	house.3SG-3SGPOSS-DAT	bring-EVPF.NPST.3SG
		'He bought two kilograms of meat from the butcher and brought it to his house.'


